apply plugin: "java"
apply plugin: "eclipse"
apply plugin: "maven"
apply plugin: "signing"
apply plugin: 'checkstyle'

//
//uploadArchives {
//    repositories {
//        add project.repositories.fileRepo
//    }
//}
//
//
//publishing {
//    publications {
//        mavenJava(MavenPublication) {
//            from components.java
//        }
//    }
//}


checkstyle {
   toolVersion ="7.8.2"
}

// project information
group = 'hu.vissy.plain-text-table'
version = rootProject.version
//archivesBaseName=rootProject.name+"-"+project.name+"-"+project.version

// dependency management as you like
repositories {
    mavenCentral()
}

// javadoc.jar generation
task javadocJar (type: Jar, dependsOn: javadoc) { 
    classifier = 'javadoc'
    from javadoc.destinationDir
}
// sources.jar generation
task sourceJar (type : Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

// summarize artifacts
artifacts {
    archives sourceJar
    archives javadocJar
}

signing {
	sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            // POM signature
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

	
            // Target repository
		    repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                	authentication(userName: project.properties.ossrhUser, password: project.properties.ossrhPassword)
		    }
		
			snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                	authentication(userName: project.properties.ossrhUser, password: project.properties.ossrhPassword)
		    }
                
            pom.project {
                name project.name
				description 'A customizable plain-text based table generator'
                packaging 'jar'
                url 'https://github.com/balage1551/plain-text-table'

				scm {
  					url 'https://github.com/balage1551/plain-text-table/tree/master'                    
 					connection 'scm:git:git://github.com/balage1551/plain-text-table.git'
  					developerConnection 'scm:git:ssh://github.com/balage1551/plain-text-table.git'
  				}
                licenses {
                	license {
                    	name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/license/LICENSE-2.0.txt'
                        distribution 'repo'
                    }
                }

                developers {
                    developer {
                        id 'Balage'
                        name 'Bal√°zs Vissy'
                        email 'balage42-maven@yahoo.com'
                    }
                }
            }
        }
    }
}


task uploadTest(dependsOn: assemble) {
    
    finalizedBy 'updateGitIfReleaseSuccessful'
    
    gradle.taskGraph.whenReady {
        if (gradle.taskGraph.hasTask(":${project.name}:uploadTest")) {
            rootProject.ext.versionUtils = new VersionUtils()
            rootProject.versionUtils.validateGitState()
            ext.lv = rootProject.versionUtils.getLastRelease()
            ext.nv = rootProject.versionUtils.getNewRelease()
            rootProject.version=nv.toVersionSequence()
            project.version=nv.toVersionSequence()
            
            println "Last version: ${lv.toVersionSequence()}" 
            println "New version: ${nv.toVersionSequence()}" 
        } else {
        }
    }
    
    doLast {
        println "Version: ${project.version}"
        rootProject.versionUtils.simulateUpload()
    }
}

task updateGitIfReleaseSuccessful {
    onlyIf {
        uploadTest.state.failure == null
    }
    
    doLast {
        println "Updating git to version: ${rootProject.version}"
        def v = rootProject.versionUtils.getNewRelease()
        rootProject.versionUtils.updateChangeLog()
        rootProject.versionUtils.updateRepository()
    }
}
